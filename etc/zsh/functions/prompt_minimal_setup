# Minimal Prompt

prompt_minimal_collapsed_dir() {
	echo "${PWD/#$HOME/\~}"
}
prompt_minimal_collapsed_basename() {
	local collapsed=${PWD/#$HOME/}
	echo "${collapsed##*/}"
}
prompt_minimal_collapsed_dirname() {
	if [[ $PWD != "$HOME" ]]; then
		local collapsed=${PWD/#$HOME/}
		echo "${collapsed%/*}/"
	fi
}

prompt_minimal_accept_line() {
	if [[ -z $BUFFER ]]; then
		return_code=$return_code_disabled
	else
		return_code=$return_code_enabled
	fi
	zle accept-line
}

if [[ ${EUID:-} -eq 0 ]]; then
	prompt_minimal_character() {
		echo '#'
	}

	prompt_minimal_hostname() {
		[[ -n $SSH_CLIENT ]] || return 0
		echo "%{$fg_bold[red]%}%m%{$reset_color%}:"
	}
else
	prompt_minimal_character() {
		echo '>'
	}

	prompt_minimal_hostname() {
		[[ -n $SSH_CLIENT ]] || return 0
		echo "%{$fg[cyan]%}%m%{$reset_color%}:"
	}
fi

prompt_minimal_setup() {
	return_code_enabled="%(?:%{$fg_bold[yellow]%}$(prompt_minimal_character):%{$fg_bold[red]%}$(prompt_minimal_character))"
	return_code_disabled="$fg_bold[yellow]$(prompt_minimal_character)"
	return_code=$return_code_enabled

	PROMPT='$(
		prompt_minimal_hostname
	)%{$fg_bold[grey]%}$(
		prompt_minimal_collapsed_dirname
	)%{$reset_color%}%{$fg_bold[cyan]%}$(
		prompt_minimal_collapsed_basename
	)%{$reset_color%} ${return_code}%{$reset_color%} '

	RPROMPT=
	PS2=
	SPROMPT="$fg[red]%R â†’$reset_color $fg[green]%r?$reset_color (Yes, No, Abort, Edit) "

	zle -N prompt_minimal_accept_line
	bindkey '^m' prompt_minimal_accept_line
}

prompt_minimal_setup "$@"
