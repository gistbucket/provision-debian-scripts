# Minimal Prompt

prompt_minimal_collapsed_dir() {
	echo "${PWD/#$HOME/\~}"
}

prompt_minimal_collapsed_basename() {
	local collapsed=${PWD/#$HOME/}

	collapsed=${collapsed##*/}

	echo "%{$fg_bold[cyan]%}${collapsed}%{$reset_color%} "
}

prompt_minimal_collapsed_dirname() {
	[[ $PWD != "$HOME" ]] || return 0

	local collapsed=${PWD/#$HOME/}

	collapsed=${collapsed%/*}

	echo "%{$fg_bold[grey]%}${collapsed}/%{$reset_color%}"
}

if [[ ${EUID:-} -eq 0 ]]; then
	prompt_minimal_character() {
		echo '#'
	}

	prompt_minimal_hostname() {
		[[ -n $SSH_CLIENT ]] || return 0
		echo "%{$fg_bold[red]%}%m%{$reset_color%}:"
	}
else
	prompt_minimal_character() {
		echo '>'
	}

	prompt_minimal_hostname() {
		[[ -n $SSH_CLIENT ]] || return 0
		echo "%{$fg[cyan]%}%m%{$reset_color%}:"
	}
fi

prompt_minimal_colored_character() {
	local color

	if [[ $prompt_minimal_exit_status = success ]] || [[ -z $prompt_minimal_exit_status ]]; then
		color=yellow
	else
		color=red
	fi

	echo "%{$fg_bold[$color]%}$(prompt_minimal_character)%{$reset_color%}"
}

preexec() {
	prompt_minimal_preexec_called=true
}

precmd() {
	if [[ $? -ne 0 ]] && [[ $prompt_minimal_preexec_called = true ]]; then
		prompt_minimal_exit_status=failure
		unset prompt_minimal_preexec_called
	else
		prompt_minimal_exit_status=success
	fi
}

prompt_minimal_setup() {
	PROMPT='$(
		prompt_minimal_hostname
	)$(
		prompt_minimal_collapsed_dirname
	)$(
		prompt_minimal_collapsed_basename
	)$(
		prompt_minimal_colored_character
	) '
}

prompt_minimal_setup "$@"
